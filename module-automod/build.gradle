plugins {
    id 'java-library'
    id 'org.springframework.boot' version "${SPRING_VERSION}"
    id 'io.spring.dependency-management' version "${SPRING_DEPENDENCY}"
    id 'io.freefair.lombok' version "${LOMBOK_VERSION}"
}

group = 'net.lindseybot'
version = rootProject.version
sourceCompatibility = targetCompatibility = '17'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('jar') {
    enabled = true
    setArchivesBaseName(project.name)
    manifest {
        attributes 'Implementation-Version': project.version
    }
}

dependencies {
    api("net.jodah:expiringmap:${EXPIRING_MAP_VERSION}")
    api("org.json:json:${ORG_JSON_VERSION}")
    api('me.xdrop:fuzzywuzzy:1.4.0')

    // Shared dependencies
    implementation project(':common')
    implementation("${JDA_SLUG}:${JDA_VERSION}") {
        exclude module: 'opus-java'
    }

    // Spring
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter')

    // Development
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

test {
    useJUnitPlatform()
}
